# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "ked"
BBFILE_PATTERN_ked := "^${LAYERDIR}/"
BBFILE_PRIORITY_ked = "10"

# Let us add layer-specific bbappends which are only applied when that
# layer is included in our configuration
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# Let us add layer-specific bbs which are only applied when that
# layer is included in our configuration
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# Let us add series-specific bbappends which are only applied if
# BASE_SERIES is set accordingly
BBFILES += "${LAYERDIR}/series-compat/${BASE_SERIES}/recipes*/*/*.bbappend"

# Let us add series-specific bbs which are only applied if
# BASE_SERIES is set accordingly
BBFILES += "${LAYERDIR}/series-compat/${BASE_SERIES}/recipes*/*/*.bb"

# Set Kontron server paths and repositories
KED_GIT_SERVER_SSH = "git@git.kontron-electronics.de"
KED_GIT_SERVER = "git.kontron-electronics.de"
KED_GIT_APPS   = "${KED_GIT_SERVER}/sw/misc/apps"
KED_GIT_USER ?= "ktn-user"
KED_GIT_PWD ?= "ktnuser"

# Set exceet server paths and repositories for compatibility
EXCEET_GIT_SERVER = "${KED_GIT_SERVER}"
EXCEET_GIT_APPS   = "${KED_GIT_APPS}"
EXCEET_GIT_USER ?= "${KED_GIT_USER}"
EXCEET_GIT_PWD ?= "${KED_GIT_PWD}"

LAYERVERSION_ked = "1"
LAYERSERIES_COMPAT_ked = "dunfell kirkstone"
LAYERDEPENDS_ked = "yocto"
