DISTRO = "ktn"
DISTRO_NAME ?= "Kontron Electronics Reference Distro"
DISTRO_VERSION ?= "${@ '0.0.0-dev' if not d.getVar('SOURCECODE_VERSION') else d.getVar('SOURCECODE_VERSION') }"
DISTRO_CODENAME ?= "ktn-dunfell"

SDK_VENDOR = "-ktnsdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${DATE}', 'snapshot')}"
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/kontron/${MACHINE}/${SDK_VERSION}"

MAINTAINER ?= "Kontron Electronics GmbH <support@kontron-electronics.de>"
TARGET_VENDOR = "-ktn"

# =========================================================================
# misc settings
# =========================================================================
PACKAGE_FEED_URIS ?= "http://192.168.1.10/"

# remove '-g' because otherwise release-builds with
# the meta-toolchain are build with debugging symbols
FULL_OPTIMIZATION:remove = "-g"

DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE}"
IMAGE_NAME = "${IMAGE_BASENAME}-${MACHINE}-${DISTRO_CODENAME}-v${DISTRO_VERSION}-${DATE}"
IMAGE_NAME[vardepsexclude] = "TIME DATE DATETIME"

DISTRO_EXTRA_RDEPENDS = "packagegroup-core-boot"
DISTRO_EXTRA_RRECOMMENDS = "kernel-module-af-packet"

GCNANO_USERLAND_USE_VENDOR_DIR = "0"

# add the manifest and the licenses file on image generated
COPY_LIC_MANIFEST = "1"

# =========================================================================
# include some basic settings copied from poky
# =========================================================================
require conf/distro/include/ktn-base.inc

# =========================================================================
# build info
# =========================================================================
BUILDCFG_VARS += "DISTRO_CODENAME"
BUILDCFG_VARS += "${@'ACCEPT_EULA_' + d.getVar('MACHINE')}"
BUILDCFG_VARS += "GCCVERSION"
BUILDCFG_VARS += "PREFERRED_PROVIDER_virtual/kernel"
BUILDCFG_VARS += "PREFERRED_VERSION_${PREFERRED_PROVIDER_virtual/kernel}"

# writes build information to target filesystem on /etc/build
IMAGE_BUILDINFO_VARS = "${BUILDCFG_VARS}"
INHERIT += "image-buildinfo"

# =========================================================================
# default/preferred providers
# =========================================================================
PREFERRED_PROVIDER_libevent = "libevent-fb"
PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg-native = "libjpeg-turbo-native"

# =========================================================================
# init system
# =========================================================================
VIRTUAL-RUNTIME_init_manager = "sysvinit"
VIRTUAL-RUNTIME_initscripts = "initscripts"

# =========================================================================
# distro features
# =========================================================================
DISTRO_FEATURES_BACKFILL_CONSIDERED += "systemd"

DISTRO_FEATURES = " \
	alsa \
	argp \
	bluetooth \
	bluez5 \
	ext2 \
	ext4 \
	gplv3 \
	gstreamer \
	ipv4 \
	ipv6 \
	largefile \
	multiarch \
	nfs \
	opengl \
	optee \
	pam \
	pulseaudio \
	usbgadget \
	usbhost \
	wifi \
	xattr \
	zeroconf \
	${DISTRO_FEATURES_LIBC} \
"
