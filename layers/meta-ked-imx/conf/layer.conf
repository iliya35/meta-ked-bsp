# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "ked-imx"
BBFILE_PATTERN_ked-imx := "^${LAYERDIR}/"
BBFILE_PRIORITY_ked-imx = "11"

# Let us add layer-specific bbappends which are only applied when that
# layer is included in our configuration
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bbappend' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# Let us add layer-specific bbs which are only applied when that
# layer is included in our configuration
BBFILES += "${@' '.join('${LAYERDIR}/%s/recipes*/*/*.bb' % layer \
               for layer in BBFILE_COLLECTIONS.split())}"

# Let us add series-specific bbappends which are only applied if
# BASE_SERIES is set accordingly
BBFILES += "${LAYERDIR}/series-compat/${BASE_SERIES}/recipes*/*/*.bbappend"

# Let us add series-specific bbs which are only applied if
# BASE_SERIES is set accordingly
BBFILES += "${LAYERDIR}/series-compat/${BASE_SERIES}/recipes*/*/*.bb"

LAYERVERSION_ked-imx = "1"
LAYERSERIES_COMPAT_ked-imx = "dunfell kirkstone"
LAYERDEPENDS_ked-imx = "ked freescale-layer"
LAYERDEPENDS_ked-imx:append:mx8m-nxp-bsp = " meta-arm"
